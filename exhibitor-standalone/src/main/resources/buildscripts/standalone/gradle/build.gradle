buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
      classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
      classpath 'com.netflix.nebula:nebula-ospackage-plugin:3.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'nebula.os-package'

group = 'exhibitor'
archivesBaseName = 'exhibitor'
version = '1.5.5'

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://repository.jboss.org/nexus/content/groups/public/"
    }
}

dependencies {
    compile 'com.netflix.exhibitor:exhibitor-standalone:1.5.5'
}

jar {
    manifest {
        attributes (
            'Main-Class': 'com.netflix.exhibitor.application.ExhibitorMain',
            'Implementation-Version': project.version
        )
    }
}

shadowJar {
    mergeServiceFiles()
}

assemble.dependsOn shadowJar

task rpm(type: Rpm) {
   dependsOn tasks.build
   group = 'Build'
   description = 'rpm for netflix exhibitor'
   release = '1'

   packageName 'exhibitor'
   packageGroup 'Application'
   version "${version}"
   release "${release}"
   vendor 'netflix'

   user = 'root'
   permissionGroup = 'root'

   arch 'noarch'
   os 'linux'

   from(tasks.shadowJar) {
       into "/opt/exhibitor"
       rename(tasks.shadowJar.outputs.files.singleFile.name, "exhibitor-standalone-${version}.jar")
   }
}
